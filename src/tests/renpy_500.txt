label alt_day4_sl_7dl_begin:
    "\"ask_you_out\""
    scene bg int_house_of_mt_sunset with dissolve
    play music music_7dl["ask_you_out"] fadein 3
    play ambience ambience_int_cabin_day fadein 2
    "Мне всю ночь снилось что-то хорошее, доброе, а потому проснулся я в самом великолепном расположении духа."
    "Потому что это доброе и хорошее всегда имело самые голубые глаза и самую добрую улыбку."
    "Вот только имя никак не мог вспомнить, как ни старался."
    "И вроде вертелось на кончике языка, вертелось, а не давалось."
    "И так я огорчился под конец, что, будто прорываясь сквозь некие тенёта, дёрнулся вперед и выпалил:"
    me "Славя!"
    "И проснулся."
    "Утреннее, эфемерное, угасающее, но заставляющее глупо улыбаться и чувствовать себя самым счастливым человеком."
    "А вот и она."
    show sl smile pioneer
    show prologue_dream
    with dissolve
    sl "С добрым утром."
    "И такой она красивой и хорошей была, что я и сам улыбнулся в ответ:"
    me "А я видел тебя во сне!"
    show sl shy pioneer with dspr
    sl "Надеюсь, это было что-то хорошее?"
    me "Очень, очень хорошее."
    me "Это же ты! Как там могло быть что-то плохое?"
    sl "Совсем засмущаешь бедную девушку."
    show sl smile pioneer close with dissolve
    "Она наклонилась ко мне и поцеловала в лоб."
    stop music fadeout 10
    pause(1)
    "Поверили, что ли?"
    "Наивные."
    with fade
    pause(1)
    if herc or loki:
        hide prologue_dream
        hide sl
        with dissolve
        "Нет, пробуждение и в самом деле было довольно приятным."
        "Я услышал чей-то воркующий голос, кто-то ненавязчиво пытался меня растолкать."
        sl "Семён… Ну, Семён… Ну просыпайся."
        me "Славя…"
        "Разулыбался я, открывая глаза."
        show sl happy sport with dspr
        sl "Проснулся? Ура!{w} А теперь — на пробежку!"
        me "Пробежку? {w}Никуда я не побегу!"
        me "И вообще, я спать!"
        sl "Серьёзно?"
        me "Имею право, дай поспать!"
        sl "Пошли, тебе понравится!"
        menu:
            "Чёрт с тобой. Дай одеться":
                play ambience ambience_forest_evening fadein 6
                play music music_7dl["runaway"] fadein 3
                play sound_loop sfx_run_forest
                scene
                $ renpy.show("bg ext_backdoor_sunset_7dl", at_list = [running], what = Dawn("bg ext_backdoor_sunset_7dl"))
                with fade2
                play sound sfx_7dl["breath"] fadein 0
                scene
                $ renpy.show("bg ext_backroad_day_7dl", at_list = [running], what = Dawn("bg ext_backroad_day_7dl"))
                with fade2
                "Да, если бы моё утро ограничивалось лишь воркованием и поглаживаниями, я был бы самым счастливым человеком на Земле!"
                "Возможно, даже поверил бы в то, что люди вокруг друг другу братья, всё вокруг замечательно, и вообще!"
                with fade
                "Но нет же, мы решили показать Славе!"
                dreamgirl "Достать и показать!"
                play sound sfx_7dl["breath"] fadein 0
                scene
                $ renpy.show("bg ext_lake_sunset_7dl", at_list = [running], what = Dawn("bg ext_lake_sunset_7dl"))
                with fade2
                "В общем, нетрудно предугадать, чем всё закончилось, раз уж я здесь потею, правда?"
                "Я бросил злой взгляд на обтянутые шортами полушария, ритмично двигающиеся в пяти метрах передо мной."
                "Взглянул совсем без эротического подтекста."
                "Пуще всего сейчас мне хотелось наподдать по ним как следует!"
                "Будто почувствовав моё желание, Славя сбавила темп и побежала рядом со мной."
                scene
                $ renpy.show("bg ext_path_day", what = Dawn("bg ext_path_day"))
                show sl smile sport
                with dissolve
                sl "Вот видишь, как хорошо! Проснуться с утра, размяться, пока остальной лагерь спит, и есть только ты и твои мысли!"
                me "Я предпочёл бы встретиться со своими мыслями, лёжа на боку."
                "Отдуваясь, пропыхтел я."
                "Сердце заполошно колотилось где-то под горлом, перед глазами всё давно потемнело, и теперь я мечтал куда-нибудь свалиться и там тихонечко издохнуть."
                "К несчастью, Славя придерживалась на этот счёт кардинально противоположного мнения."
                "Она запрыгала вокруг меня, тормоша:"
                show sl happy2 sport
                with dspr
                sl "Не притворяйся умирающим лебедем! {w}Я же вижу, что тебе нравится!"
                th "Так нравится, что спасу нет."
                "Нет, если бы мы просто неторопливо потрясли жирок по дорожкам лагеря, я бы, может, даже возмущаться не стал!"
                "Так ведь нет!"
                "Славя вывела меня на некий неведомый «большой круг», включающий в себя периметр лагеря с заходом на берег, и припустила по нему так, будто за ней гналась бешеная собака."
                "Так вот, это возмутило меня до глубины души."
                "И я сейчас буду возмущаться!"
                "Да. Сразу, как только отдышусь."
                sl "Ну ты чего? Каши мало ел?"
                th "Я с утра ещё вообще ничего не ел."
                sl "До ворот двести метров, побежали!"
                hide sl with dissolve
                "Славя ободряюще кивнула мне и ускорилась в сторону видимой отсюда стены лагеря."
                "А мне, так как я вовсе не горел желанием поселиться здесь на веки вечные, ничего не оставалось, как последовать за ней."
                me "Славя…"
                "Пропыхтел я."
                dreamgirl "Ну погоди!"
                $ alt_day4_sl_7dl_workout = True
                $ lp_sl += 1
                $ karma += 15
                stop sound_loop
                stop music fadeout 6
                pause(1)
            "Да отстанешь ты?!":
                stop music fadeout 6
                show sl sad sport with dspr
                "Славя вздохнула и отошла на полшага."
                sl "Я думала, тебе должно понравиться, но раз ты такой бука…"
                play sound sfx_close_door_1
                scene bg int_house_of_mt_sunset
                with fade2
                "Я не очень любил ругаться, но раз уж мне приходилось отстаивать своё право на подъём не раньше восьми утра…"
                "Кинув взгляд на часы, я застонал — до подъёма было ровно пятнадцать минут."
                th "Совсем совести у людей нет!"
                "Я провалился в дрёму."
    else:
        scene bg int_house_of_mt_sunset
        with fade2
        "На самом деле, эротические грёзы с одной златовлаской так и остались плодом моего воспалённого либидо."
        "Которому, впрочем, не дали особо разгуляться."
        "Сначала где-то что-то зазвонило, потом раздался грохот, будто кто-то упал с кровати на пол."
        "Приглушённый нецензурный возглас, шорох второпях надеваемой одежды."
        "И меня без особой ласки потрясли за плечо:"
        mt "Проснись и пой, Семён! А твоя вожатая побежала на летучку, молясь по дороге, что её не пришибут за опоздание!"
        play sound sfx_close_door_1
    scene bg ext_washstand2_day
    with joff_l
    play music music_list["everyday_theme"] fadein 5
    pause(1)
    play sound sfx_open_water_sink
    pause(1)
    play sound_loop sfx_water_sink_stream
    me "А-а-а-а-а!" with vpunch
    "Те же, там же."
    "Всё как в старых пьесах."
    "Разве что в этот раз нигде рядом не наблюдалось одного мелкого источника неприятностей."
    "И никаких общественных волнений, комиссий и осмотров."
    "А потому все глупости сегодня будут злонамеренными."
    th "Вне размеров ежедневной квоты глупостей, конечно."
    "Я всегда считал, что человеку в день отведено строго определённое количество бестолковых и раздражающих телодвижений, выполнять которые вредно, противно — но надо!"
    "Потому что, если ты не делаешь глупости злонамеренно, они начинают происходить с тобой, когда им вздумается!"
    scene bg ext_washstand_day
    with flash
    if alt_day4_sl_7dl_workout:
        dreamgirl "Расскажи мне лучше, по кой пёс ты за Славяной увязался на пробежку?"
        dreamgirl "В конце концов, её потребности не высыпаться — это только её потребности!"
        th "С чего ты думаешь, что она не выспалась? Припухшие веки, прочая фигня?"
        dreamgirl "Мда, беда… Был бы ты хотя бы внешне красивый, это тебя извинило бы. {w}Так ведь нет!"
        dreamgirl "Вы разошлись по койкам в одно и то же время, легли примерно одинаково."
        dreamgirl "Ты выспался? {w}То-то и оно."
        dreamgirl "Надо было замотать её в покрывало и спрятать в шкаф — и ложиться досыпать!"
        th "Старый Мазай разболтался в сарае."
        th "Ложись себе да досыпай, что ты как этот?"
        dreamgirl "О, ну разумеется! Ты бодрствуй, наблюдай за пасторалями в чёрном спортивном костюме — а я спи? {w}У Пипите Хуарто не опухло ли хлебало?"
    "Я плеснул в лицо ещё одну горсть ледяной воды."
    "С каким-то даже мазохистским удовлетворением."
    th "И не выспался, и не отдохнул, и… Эх, вот тебе ещё ледяной воды за шиво… Оу!"
    play sound sfx_close_water_sink
    stop sound_loop
    "За шиворот вполне ожидаемо проложили путь ледяные дорожки."
    scene bg ext_houses_sunset
    with dissolve
    "Повторяя как мантру «ни одна сволочь не испортит мне утро», я вернулся домой."
    scene bg ext_house_of_mt_sunset
    with dissolve
    "И, конечно же, у самого порога столкнулся с надежей и опорой, добрейшей и прочая, и прочая…"
    show mt smile sport
    with dissolve
    if alt_day4_sl_7dl_workout:
        mt "Ты меня сегодня удивляешь! Неужели сам поднялся спозаранку?"
        me "Вы слишком хорошего мнения обо мне."
    else:
        me "И как оно?"
        show mt surprise sport with dspr
        mt "Оно?"
        if dr:
            me "Вы на летучку опоздали."
            "Напомнил я."
            show mt smile sport with dspr
            mt "Всё хорошо. Тем более, никто об этом никогда не узнает."
            mt "Ведь{w=.3} прав{w=.2}да?"
            "В её улыбке я разглядел нечто зловещее."
            "Потому поспешил кивнуть."
            mt "Рассчитываю на тебя."
        else:
            me "Настроение."
            mt "Настроение отправить тебя на зарядку!"
    mt "Давай сюда свои мыльно-рыльные и двигай на площадку, разминаться."
    "К этому моменту я как раз восстановил дыхание и сердцебиение, и ни в каких разминках уже не нуждался."
    "Но что-то подсказывало мне, что с вожатой сейчас лучше не спорить."
    "То ли дело было в отчаянии во взгляде, то ли в чём-то ещё, но я покорно кивнул, всучил ей свёрток и, сделав через левое плечо, отчалил."
    "Туда, где разогретая уже Славя машет руками и двигает тазом так, будто она меня спасла и в благородство играть не будет."
    "На зарядку!"
    pause(1)
    scene bg ext_square_sunset3_7dl
    with dissolve
    play ambience ambience_camp_center_day fadein 3
    "Чтобы быть здоров — закаляйся! Голым пузом ляг на гвоздь и вращайся!"
    "Моя третья зарядка отличалась от второй тем, что на этот раз народа почему-то было ощутимо больше!"
    "Нет, дело было совсем не в том, что вожатые в этот раз сработали лучше и подняли больше пионеров."
    if alt_day4_sl_7dl_workout:
        "Видимо, проснулся я ещё не совсем, несмотря даже на пробежку и умывание под холодной водой."
    else:
        "Видимо, проснулся я не до конца, несмотря на умывание ледяной водой."
    "Иначе как можно объяснить наличие в рядах пионеров морд лиц чуть постарше — лет эдак тридцати-сорока."
    me "Брррр!"
    "Я закрыл глаза и помотал головой."
    "«Старые» пионеры никуда не делись."
    show dv smile sport
    with dissolve
    dv "Что головой трясёшь, пил вчера?"
    me "Кто все эти люди?"
    show dv smile sport
    with dspr
    dv "Сегодня же роддень, забыл?"
    me "Скорее, не знал. {w}А здесь-то они зачем?"
    dv "Правила такие."
    "Пожала плечами Алиса."
    dv "Хочешь быть с дитём на территории лагеря — носи форму."
    me "Дресс-код, что ли?"
    show dv grin sport
    with dspr
    dv "Я твоё буржуйское наречье не разумею. Ты не умничай, пальцем покажи."
    me "Что все носят, то и родители носят."
    dv "Так и есть."
    hide dv smile sport
    "Славя скомандовала что-то, и Алиса встала рядом со мной, в такт ведущей размахивая конечностями."
    "Ряды подравнялись, родители дисциплинированно пристроились с дальней стороны площади."
    show sl smile sport
    with dissolve
    sl "Начинаем физзарядку! Ноги на ширине плеч, рывки руками, и — раз!"
    "Все замахали руками, видимо, втайне надеясь улететь отсюда к родным кроваткам."
    "А мне…"
    "Что ж, я овладел этим сверхсекретным умением ещё в 18 лет."
    "Способностью смириться с тем, что я встал, и сегодня уже не сплю."
    "Очень полезное умение!"
    "Славя вела зарядку на всех, но сама то и дело бросала взгляд в мою сторону."
    show sl happy sport
    with dspr
    "Должно быть, ей не нравилось отсутствие энтузиазма на моём лице."
    "Потому что она всеми силами на свой манер пыталась подбодрить меня."
    th "Зря стараешься. Единственный способ, которым ты могла бы взбодрить меня как следует, в лагере лучше не использовать."
    dreamgirl "Используй воображение, Люк!"
    "Это предложение не было лишено определённого смысла."
    "Ибо форма на Славе показывала то, что надо было показывать, и одновременно оставляла уйму пространства для фантазий."
    th "Единственное светлое пятно этого утра."
    "Обречённо подумал я, когда Славя отпустила пионеров и подбежала ко мне."
    "Со вполне очевидным предложением, из тех, от которых не отказываются."
    show sl laugh sport
    with dspr
    if alt_day4_sl_7dl_workout:
        sl "Эта будет по малому кругу."
        "Утешила, нечего сказать."
    sl "Побежали!"
    hide sl with moveoutright
    stop music fadeout 3
    stop ambience fadeout 6
    with fade
    return

label alt_day4_sl_7dl_breakfast:
    scene bg ext_square_sunset3_7dl with dissolve
    play music music_7dl["carefree"] fadein 3
    play ambience ambience_camp_center_evening fadein 3
    if alt_day4_sl_7dl_workout:
        "Из-за очередной внеплановой пробежки чувствовал я себя не самым лучшим образом."
        "Так что творящееся на линейке я воспринимал с трудом."
        "Перед глазами всё плыло, каждое движение век сопровождалось вспыхивающими разноцветными кругами."
    else:
        "Я совершенно не выспался."
        if herc or loki:
            "Казалось бы, и недоспал всего-то пятнадцать минут — а вот."
            "Я недовольно покосился на Славю."
            th "Чтоб тебе три года не высыпаться, тогда поймёшь!"
            "Я покачал головой — меня немного «вело», и, судя по периодическим взглядам окружения, выглядел я не самым лучшим способом."
    "А вот Славя цвела и радовалась жизни, пробежав километровый отрезок вдоль внутреннего периметра лагеря, даже не вспотев."
    th "Ждёт тебя, касатик, абзац. В четыре строчки."
    "Славя стояла рядом, чуть улыбаясь каждый раз, когда я на неё смотрел."
    "Будто чувствовала чутьём своим бабским поток внимания."
    "Не самого ласкового, должен сказать."
    "И в то же время — мне и в голову не приходило послать её по матушке и потребовать держаться от меня подальше."
    dreamgirl "Сём Сёмыч, одиночка и бездарь, нашёл себе человека, который сможет решить всё за него и пинками погнать к лучшей жизни?"
    th "Примерно так. {w}Почему-то мне кажется, что от Слави никакой гадости ждать не стоит."
    th "Внешность, безусловно, обманчива, но она ведёт себя не как потенциальный гад и вражина."
    "Нас перетасовали, уплотнили немного, из-за чего привычное уже стройное каре со стороной в 2 отряда немного смешалось, теснясь."
    "Освобождая место «пионерам» тридцати-сорокалетнего возраста, которые встали сразу за пятым отрядом."
    "Ольга поморщилась, глядя на это безобразие, но ничего не сказала."
    "Видимо, не решилась — всё же была она младше большинства «новеньких»."
    "Пришлось вести линейку как есть, без дополнительных вводных и занятий по строевой."
    "Потому и слышно было через пень-колоду — стеснялась использовать свою лужёную глотку."
    "Напрасно стеснялась, должен сказать: мне удалось расслышать только окончание её речи."
    mt "Отряды, напра… во! За пятым отрядом в столовую шагом арш!"
    scene bg ext_dining_hall_near_day
    with dissolve
    if alt_day4_sl_7dl_workout:
        th "Я слишком стар для всей этой чуши. Меня нельзя без подготовки вытаскивать из тёпленькой постельки и заставлять кроссить сначала четыре, а потом и ещё полтора километра."
        dreamgirl "Зато какова награда!"
        th "Да в гробу я такую награду видел. Подумаешь, полюбоваться дали формами в обтягайке."
        dreamgirl "Не понравилось? Ослика не устроила морковка?"
        "Я ничего не ответил, неприязненно покосившись на «морковку», стоящую рядом."
    show sl smile pioneer
    with dissolve
    sl "Как самочувствие?"
    th "Садовая роза, цветёт и пахнет. {w}И раздражает."
    me "Плохо."
    show sl laugh pioneer with dspr
    sl "Я знала, что тебе понравится."
    sl "У меня хорошие новости."
    if alt_day4_sl_7dl_workout:
        me "Что, даже лучше утренней пробежки?"
    else:
        me "Ещё лучше нашего нового «отряда»?"
    sl "А как же!"
    play sound sfx_open_dooor_campus_1
    pause(1)
    scene bg int_dining_hall_people_sunset_7dl
    with dissolve
    play ambience ambience_dining_hall_full fadein 5
    "Славя потянула дверь и зашла в столовую, пришлось топать вслед."
    "И там оказалось на удивление пусто, несмотря даже на уплотнившие наши ряды морды."
    show sl normal pioneer with dissolve
    sl "Родительский день."
    "Ответила Славя на невысказанный вопрос."
    sl "Большинство ребят забрали родители. {w}Хотя мне это и непонятно — зачем встречаться сейчас, если через три дня всё равно отъезд."
    me "Новость за новостью."
    "Девушка взяла поднос и отправилась к раздаче, кивком приглашая меня следовать за ней."
    sl "А мои приезжали совсем рано, до подъёма ещё, так что я с ними уже повидалась, и меня ничто не будет отвлекать."
    me "Отвлекать от чего?"
    show sl smile pioneer with dspr
    sl "Видел новый отряд?"
    me "Их тяжело не заметить."
    sl "Это комиссия, они будут ходить по лагерю и проверять. {w}А меня приставили к ним провожатой и помощницей."
    me "Проверять, чтобы пионеры черники зелёной не наелись?"
    show sl smile2 pioneer with dspr
    sl "В этом году «Совёнок» претендует на звание места образцового отдыха и досуга. {w}Такие звания просто так не дают."
    me "Ахха! Вот, значит, какие у тебя были хорошие новости!"
    "Я с энтузиазмом потёр руки."
    me "Ты занята, значит, я могу делать всё, что хочу!"
    show sl laugh pioneer with dspr
    sl "А тебе так не терпится избавиться от меня?"
    th "Да!"
    me "Просто я сюда приехал отдыхать! Значит, вам меня не надо загружать!"
    show sl grin pioneer with dspr
    sl "Ты лентяй, ты знаешь?"
    "Открыла Америку…"
    show sl normal pioneer with dspr
    me "Это ничего! Зато ты работаешь за семерых."
    me "Если нас сложить, получится один нормальный человек."
    "Славя сверкнула глазами:"
    show sl serious pioneer with dspr
    sl "Вообще-то, у меня есть для тебя одно дело!"
    "Судя по серьёзному взгляду, дело было калибра уборки площади, никак не меньше!"
    me "Какое?"
    sl "Колонки после вчерашних танцев. Они всё ещё стоят на площади, а в обед у нас концерт."
    me "И ты хочешь заставить меня таскать их все?"
    show sl smile pioneer with dspr
    sl "Один ты, боюсь, не справишься, так что я тебе несколько мальчиков из второго отряда пришлю."
    if herc or loki:
        menu:
            "Пусть Шварценеггер таскает!" if loki:
                $ karma -= 10
                show sl serious pioneer with dspr
                sl "Неужели помочь не хочешь?"
                me "Как ты догадалась?"
                show sl surprise pioneer with dspr
                sl "Я думала, ты не против тоже принять участие в жизни лагеря."
                me "Я принимаю в ней участие, даже лёжа на боку."
            "А других назначений нет?":
                $ alt_day4_sl_7dl_help1 = True
                show sl smile2 pioneer with dspr
                sl "Есть! Но они все будут после того, как закончите с аппаратурой."
                me "И где я их забрать должен буду?"
                sl "Да найдёшь меня где-нибудь, я буду на территории."
                me "Отказаться нельзя?"
                show sl laugh pioneer with dspr
                sl "Нет!"
            "За тобой должок!" if herc:
                $ alt_day4_sl_7dl_help1 = True
                $ karma += 10
                show sl laugh pioneer with dspr
                sl "Откуплюсь, чем только пожелаешь!"
                me "А я знаю, чего пожелаю!"
                "Была у меня одна интересная идейка."
                show sl smile2 pioneer with dspr
                sl "И чего же?"
                me "А вот когда закончу, тогда и узнаешь!"
                "Я сделал гордый и непокорённый вид, вынуждая девушку снова улыбнуться."
                sl "Надеюсь, это не будет что-то ужасное. Вылезать на стол и кукарекать я не буду, так и знай!"
                me "А и не надо. Как там зовут моих будущих помощников?"
                show sl normal pioneer with dspr
                sl "Даня и его друзья."
                me "Да, тяжело людям с таким именем. И его друзья."
                "Глубокомысленно протянул я."
                show sl smile pioneer with dspr
                sl "Они постоянно ходят вместе, не перепутаешь!"
                sl "И, если я ничего не путаю, ты их знаешь!"
    else:
        $ alt_day4_sl_7dl_help1 = True
        $ karma += 10
        me "Хорошо."
        "Вздохнул я."
        me "Круглое таскать, квадратное катать. Как прикажете."
        show sl smile2 pioneer with dspr
        sl "Ну что такое? Неужели помочь не хочешь?"
        "Не особо, конечно. Но попробуй-ка скажи такое."
        sl "Вот видишь, сам всё понимаешь."
    "Наконец, подошла и наша очередь, а нам выставили на подносы по тарелке риса с мясом и стакану бледного чая."
    dreamgirl "А в тюрьме макароны дают!"
    th "Стебись-стебись."
    show sl normal pioneer with dissolve
    "Славя, впрочем, и бровью не повела."
    "Конечно, я слышал её рассказ про семью и то, что она сама готовить умеет."
    "Но как-то слабо это коррелировало со способностью есть такой вот холостяцкий завтрак."
    "Не слушая и не понимая моих рассуждений, Славя с крайне довольным видом зачерпнула первую ложку."
    "Неприхотливость во все поля."
    "Или, скорее даже, неизбалованность."
    me "Могли бы и кашу нормальную сварить."
    "Пробурчал я, с недоверием разглядывая горку на тарелке."
    sl "Да ты попробуй, знаешь, как вкусно!"
    me "Вообще-то, ведущими собаководами рекомендовано готовить на завтрак молочную продукцию и каши на молоке, чтобы зарядить организм."
    show sl laugh pioneer with dspr
    sl "Брюзжишь как старый дед. {w}Ты попробуй, тебе понравится."
    "Не найдя понимания и здесь, я со вздохом зачерпнул содержимое тарелки."
    "И в целом Славя оказалась не так уж и неправа!"
    "Хотя завтракать бефстрогановым, по мне — то ещё извращение."
    me "А почему родичи так рано приезжали?"
    "Уточнил я, прожевав первую ложку."
    show sl upset pioneer with dspr
    sl "Они очень занятые, за братьями глаз да глаз нужен."
    me "Так брали бы их с собой."
    dreamgirl "Точно. Шесть человек на переднее сиденье."
    me "Увиделись бы."
    show sl normal pioneer with dspr
    sl "Да, по-моему, мы и так очень скоро увидимся. Правда, ненадолго."
    me "По причине?"
    "Славя довольно улыбнулась:"
    show sl smile2 pioneer with dspr
    sl "Мои труды заметили, пригласили в следующей смене поработать помощницей вожатой!"
    sl "Так что я здесь ещё немного задержусь."
    me "Тебе нравится эта идея, похоже."
    show sl happy pioneer with dspr
    sl "Конечно, нравится! Я люблю этот лагерь, и мне нравится, когда в нём всё хорошо."
    sl "Ты, кстати, тоже мог бы попробовать попроситься."
    me "И ещё три недели таскать колонки?"
    "Физические усилия меня не сказать чтобы сильно пугали, однако делать это на постоянной основе показалось мне не слишком здравой затеей."
    show sl happy pioneer with dspr
    sl "Да! Разве не здорово?"
    me "Нет."
    show sl happy2 pioneer with dspr
    sl "Семён, ты такой шутник!"
    me "Я не шучу. Ладно, пока ты здесь по путёвке, можно развлекаться таким образом."
    me "Но заниматься этим на постоянной основе — увольте. Людям за это вообще-то деньги платят!"
    show sl sad pioneer with dspr
    sl "Ты меня, конечно, извини, но чья бы корова мычала!"
    me "А если на русском?"
    show sl normal pioneer with dspr
    sl "Ты думаешь, Алексей Максимович просто так заявку подал, потому что ему скучно и не хватает развлечений в жизни?"
    me "Я-то откуда знаю? Я и про Алексея Максимовича слышу впервые в жизни, может, и скучно ему!"
    show sl sad pioneer with dspr
    "Славя вздохнула и начала терпеливо пояснять:"
    sl "Алексей Максимович — это директор нашего лагеря, заявку на присвоение звания образцового места отдыха подал он."
    sl "И подал не просто так!"
    me "От нечего делать?"
    show sl angry pioneer with dspr
    sl "Да из-за тебя!"
    "Здесь я даже не нашёлся с ответом."
    th "Оказывается, вся эта шумиха и сутолока — моя вина?"
    dreamgirl "Да, это твоя суперсила, человек-косяк!"
    show sl dontlike pioneer with dspr
    sl "Это же твои родители в прошлый раз стали спрашивать — почему, мол им приходиться отправлять тебя в лагерь, который даже не образцовый?"
    sl "Вчера, вот, тоже позвонили."
    me "Так выгнали бы меня — всего и делов."
    show sl normal pioneer with dspr
    sl "Ты предлагаешь выгнать тебя на одном лишь основании, что твои родители странные вопросы задают?"
    "Славя протянула руку и потрогала мой лоб."
    sl "Хм, лоб холодный."
    sl "В любом случае! Раз всё происходящее косвенно и из-за тебя тоже, мне кажется справедливым, что ты примешь участие в уборке и прочем таком."
    me "Ты виноват уж в том, что хочется мне кушать…"
    sl "Прости, что?"
    if alt_day4_sl_7dl_help1:
        "Я постарался сделать улыбку не слишком вымученной."
        me "Владей, что я могу сказать."
        sl "Не беспокойся, там не очень много."
        sl "И тебе на пользу! Вот, за завтрак сказал больше слов, чем вчера за весь день."
        sl "Мы ещё сделаем из тебя человека."
        me "Нисколько в этом не сомневаюсь."
    else:
        me "Ровным счётом ничего."
        me "Сама меня припахать норовишь, а чем в это время займёшься?"
        show sl smile pioneer with dspr
        sl "Проведу экскурсию по лагерю, соберу все замечания и в целом буду помогать комитету сегодня."
        me "А почему это тебе самая лёгкая работа достаётся?"
        "Девушка посмотрела мне в глаза и медленно кивнула:"